apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

ext.publishProjects = subprojects.findAll { it.name.startsWith('infoarchive-sip-sdk-') }

// OSSRH requires both javadoc and sources jars
publishProjects.each { prj ->
  prj.task('javadocJar', type: Jar) {
    from prj.javadoc
    classifier 'javadoc'
  }
  
  prj.task('sourcesJar', type: Jar) {
    from prj.sourceSets.main.allSource
    classifier 'sources'
  }
  
  prj.assemble.dependsOn prj.sourcesJar, prj.javadocJar
  
  artifacts {
    archives prj.jar, prj.javadocJar, prj.sourcesJar
  }
}

// OSSRH also requires everything to be signed
signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

// Set nexusUsername/Password in ~/.gradle/gradle.properties or specify on command line using -P
ext {
  ossrhUsername = project.hasProperty('nexusUsername') ? nexusUsername : ''
  ossrhPassword = project.hasProperty('nexusPassword') ? nexusPassword : ''
}

uploadArchives {
  finalizedBy closeAndPromoteRepository
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      
      publishProjects.each { prj ->
        // Separate out artifacts
        addFilter(prj.name) { artifact, file ->
          artifact.name == prj.name
        }
        
        pom(prj.name) {
          // OSSRH also requires certain info in the POM
          project {
            name prj.name
            description prj.description
            packaging 'jar'
            url 'https://github.com/Enterprise-Content-Management/infoarchive-sip-sdk'
    
            scm {
              connection 'https://github.com/Enterprise-Content-Management/infoarchive-sip-sdk.git'
              developerConnection 'https://github.com/Enterprise-Content-Management/infoarchive-sip-sdk.git'
              url 'https://github.com/Enterprise-Content-Management/infoarchive-sip-sdk.git'
            }
    
            licenses {
              license {
                name 'Mozilla Public License Version 2.0'
                url 'https://www.mozilla.org/en-US/MPL/2.0/'
              }
            }
    
            developers {
              developer {
                id = 'ray'
                name = 'Remon Sinnema'
                organization = 'OpenText'
                email = 'rsinnema@opentext.com'
                timezone = 'Europe/Amsterdam'
              }
            }
            
            contributors {
              contributor {
                name = 'Chandramouli Addaguduru'
                organization = 'OpenText'
              }
              contributor {
                name = 'Roman Kochkar'
                organization = 'Saint Petersburg State University'
              }
              contributor {
                name = 'Artem Kovalev'
                organization = 'OpenText'
              }
              contributor {
                name = 'Erik Silkensen'
                organization = 'Flatirons Solutions'
              }
              contributor {
                name = 'Tord Svensson'
                organization = 'Dell Technologies'
              }
            }
          }
          // For some reason the dependencies don't come across, so do it manually
          withXml { 
            def dependenciesNode = asNode().appendNode('dependencies')
            prj.configurations.compile.allDependencies.each {
              def dependencyNode = dependenciesNode.appendNode('dependency')
              dependencyNode.appendNode('groupId', it.group)
              dependencyNode.appendNode('artifactId', it.name)
              dependencyNode.appendNode('version', it.version)
            }
          }
        }
      }
    }
  }
}
