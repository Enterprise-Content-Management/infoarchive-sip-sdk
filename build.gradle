buildscript {
  repositories {
    maven { url mavenRepo }
    maven { url 'https://plugins.gradle.org/m2/' }
    mavenCentral()
  }
  if (!project.hasProperty('compileOnly')) {
    dependencies {
      classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3'
    }
  }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.47.0'
  id 'com.github.spotbugs' version '6.0.18' apply false
  id 'maven-publish'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
  id 'ca.cutterslade.analyze' version '1.9.2'
}

ext.SpotBugsTask = com.github.spotbugs.snom.SpotBugsTask

allprojects {
  repositories {
    maven { url mavenRepo }
  }
 }  

import org.gradle.api.logging.StandardOutputListener

apply plugin: 'eclipse'
apply plugin: 'idea'


subprojects {
  apply plugin: 'java-library'
  apply plugin: 'distribution'
  apply plugin: 'ca.cutterslade.analyze'

  sourceCompatibility = 17

  ext.PmdTask = org.gradle.api.plugins.quality.Pmd
  ext.SpotBugsTask = com.github.spotbugs.snom.SpotBugsTask
  ext.Confidence = com.github.spotbugs.snom.Confidence
  ext.Effort = com.github.spotbugs.snom.Effort

  analyzeClassesDependencies {
    warnUsedUndeclared = true
    warnUnusedDeclared = true
    logDependencyInformationToFiles = true
  }

  analyzeTestClassesDependencies {
    warnUsedUndeclared = true
    warnUnusedDeclared = true
    logDependencyInformationToFiles = true
  }

  configurations {
    commonsBeanutils {
      exclude module: 'commons-collections'
    }
    commonsCodec
    commonsCollections
    commonsIo
    commonsLang
    evoInflector
    httpclient
    httpmime
    jacksonCore
    jacksonDatabind
    jacksonDatabindXml
    jacksonDataType
    json
    semVer
    validation
    xmlunit
    yaml

    permitUnusedDeclared.extendsFrom compileOnlyApi
    permitUsedUndeclared.extendsFrom api, implementation, runtime
  }

  dependencies {
    commonsBeanutils "commons-beanutils:commons-beanutils:$commonsBeanutilsVersion"
    commonsCodec "commons-codec:commons-codec:$commonsCodecVersion"
    commonsCollections "org.apache.commons:commons-collections4:$commonsCollectionsVersion"
    commonsIo "commons-io:commons-io:$commonsIoVersion"
    commonsLang "org.apache.commons:commons-lang3:$commonsLangVersion"
    evoInflector "org.atteo:evo-inflector:$evoInflectorVersion"
    httpclient "org.apache.httpcomponents:httpclient:$httpclientVersion"
    httpmime "org.apache.httpcomponents:httpmime:$httpmimeVersion"
    jacksonCore "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    jacksonDatabind "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    jacksonDatabindXml "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
    jacksonDataType "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion", 
                    "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    json "org.json:json:$jsonVersion"
    semVer "com.github.zafarkhaja:java-semver:$semVerVersion"
    validation "javax.validation:validation-api:$validationVersion"
    yaml "org.yaml:snakeyaml:$snakeYamlVersion"

    api libs.jaxb.api
    permitUnusedDeclared libs.jaxb.api

    compileOnlyApi 'com.google.code.findbugs:jsr305:3.0.2',
      'com.github.spotbugs:spotbugs-annotations:4.7.0'
    
    testImplementation libs.bundles.mockito
    testRuntimeOnly libs.junit.engine
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Xlint:-processing' << '-Xlint:-serial'
  }
  
  javadoc {
    def events = []
    StandardOutputListener listener = { event -> events << event }
    doFirst {
      logging.addStandardOutputListener(listener)
    }
    doLast {
      logging.removeStandardOutputListener(listener)
      if (events.find { it.toString() =~ /warning/ }) {
        throw new GradleException('Warnings in Javadoc, see output above')
      }
    }
  }

  // Need all test info on command-line for TravisCI
  test {
    outputs.upToDateWhen { false } // Always run tests so that we have a test report on each build
    testLogging {
      events 'failed'
      showStandardStreams = true
      showStackTraces = true
      exceptionFormat 'full'
    }
    useJUnitPlatform {
      if (project.hasProperty('testExcludeTags')) {
        excludeTags testExcludeTags
      }
    }
    maxParallelForks = (Runtime.runtime.availableProcessors() * 2).intdiv(3) ?: 1
  }

  if (!project.hasProperty('compileOnly')) {
    apply plugin: 'checkstyle'
    checkstyle {
      configFile = rootProject.file('config/checkstyle.xml')
      toolVersion = '8.45.1'
      ignoreFailures = false
    }
  
    // Despite ignoreFailures=false, the build won't by default fail on CheckStyle warnings
    // https://issues.gradle.org/browse/GRADLE-2888
    tasks.withType(Checkstyle).each { checkstyleTask ->
      checkstyleTask.doLast {
        reports.all { report ->
          def outputFile = report.outputLocation.asFile.get()
          if (outputFile.exists() && outputFile.text.contains('<error ')) {
            throw new GradleException("Found checkstyle issues in $outputFile")
          }
        }
      }
    }

    apply plugin: 'pmd'

    pmd {
      ruleSets = []
      ruleSetFiles = rootProject.files('config/pmd.xml')
      toolVersion = '6.38.0'
      ignoreFailures = false
    }
  
    apply plugin: 'com.github.spotbugs'

    dependencies {
      spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0',
        'com.mebigfatguy.fb-contrib:fb-contrib:7.4.7'
    }
    
    spotbugs {
      reportLevel = Confidence.LOW
      toolVersion = '4.8.3'
      ignoreFailures = false
      effort = Effort.MAX
      excludeFilter = rootProject.file('config/spotbugs_filter.xml')
    }
    
    tasks.withType(SpotBugsTask) {
      reports {
        html {
          enabled = true
        }
        xml {
          enabled = true
        }
      }
    }

    apply from: rootProject.file('jdepend.gradle')
//    apply from: rootProject.file('coverage.gradle')
  
    apply plugin: 'eclipse'

    eclipse.classpath {
      defaultOutputDir = file('classes')
      file.whenMerged { classpath ->
        classpath.entries.removeAll { it.kind == 'lib' && it.path.contains("/$buildDir.name/") }

        classpath.entries.findAll {
          it instanceof org.gradle.plugins.ide.eclipse.model.ProjectDependency
        }.each { entry ->
          entry.entryAttributes.without_test_code = project.name.endsWith('-yaml')
        }
      }
    }
  
    apply plugin: 'idea'
  }
  
  distributions {
    main {
      contents {
        from(jar) {          
          into 'libs'
        }
        from(configurations.runtimeClasspath) {
          into 'libs'
        }
      }
    }
  }
}

if (!project.hasProperty('compileOnly')) {
  apply plugin: 'codenarc'

  codenarc { toolVersion = '1.6.1' }
  
  task codenarc(type: CodeNarc) {
    mustRunAfter allprojects*.tasks*.findByPath('clean') - null
    configFile = file('config/codenarc.groovy')
  
    reports {
      html {
        outputLocation = rootProject.file('build/reports/codenarc.html')
        required = true
      }
      xml {
        outputLocation = rootProject.file('build/results/codenarc.xml')
        required = true
      }
    }
  
    source = fileTree(rootDir) {
      include '**/*.gradle'
      include '**/*.groovy'
      exclude '**/build/**/*.gradle'
      exclude '**/build/**/*.groovy'
    }
  }
}

project(':infoarchive-yaml') {
  description = 'InfoArchive YAML library'
  configurations {
    api.extendsFrom commonsIo, commonsLang, evoInflector, yaml, json
  }
}


project(':infoarchive-sdk-core') {
  description = 'InfoArchive SDK core library'

  configurations {
    api.extendsFrom commonsBeanutils, commonsCollections, commonsCodec, commonsIo, commonsLang, evoInflector, 
        httpclient, httpmime, jacksonCore, jacksonDataType, jacksonDatabind, semVer, validation, yaml
  }
  
  dependencies {
    api project(':infoarchive-yaml')
    testImplementation project(':infoarchive-yaml').sourceSets.test.output,
      libs.awaitility,
      'org.xmlunit:xmlunit-core:' + xmlunitVersion
  }

  javadoc {
    def overview = file('src/javadoc/overview.html')
    title = "InfoArchive SIP SDK $version"
    options.overview = overview
    inputs.file overview
  }
}

project(':infoarchive-sdk-velocity') {
  description = 'InfoArchive SDK velocity library'

  configurations {
    velocity
    api.extendsFrom commonsCollections, velocity
  }

  dependencies {
    velocity "org.apache.velocity:velocity-engine-core:$velocityVersion"
    api project(':infoarchive-sdk-core')
    testImplementation project(':infoarchive-yaml').sourceSets.test.output
  }
}

defaultTasks 'build'

task check {
  dependsOn subprojects*.tasks*.check
}

if (!project.hasProperty('compileOnly')) {
  apply from: rootProject.file('publish.gradle')

  // [sonarToken] is encrypted in .travis.yml, so only available when building in Travis
  if (project.hasProperty('sonarToken')) {
    System.setProperty('sonar.login', sonarToken)
    apply plugin: 'org.sonarqube'
    subprojects.findAll { 
      it.name.startsWith('sample')
    }.each {
      it.sonarqube.skipProject = true
    }
    check.dependsOn 'sonarqube'
  }
  
  check.dependsOn tasks.codenarc
}

